buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'io.freefair.lombok' version '6.6.1'
    id 'java'
    id 'jacoco'
    id 'idea'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("libName")

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/public/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${mcVersion}-R0.1-SNAPSHOT"
    compileOnly 'net.md-5:bungeecord-api:1.19-R0.1-SNAPSHOT'

    compileOnly 'org.jetbrains:annotations:23.1.0'
    compileOnly 'commons-codec:commons-codec:1.15'
    compileOnly 'commons-io:commons-io:2.11.0'
    compileOnly 'commons-lang:commons-lang:2.6'
    compileOnly 'org.mongodb:mongodb-driver:3.12.11'
    compileOnly 'com.mojang:authlib:1.11'
    compileOnly 'me.clip:placeholderapi:2.11.2'
    implementation 'de.tr7zw:item-nbt-api-plugin:2.11.0'
    implementation 'com.github.cryptomorin:XSeries:9.2.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.17:1.13.0'
    testImplementation 'org.assertj:assertj-core:3.23.1'
}

java {

}

shadowJar {
    classifier = ''
    exclude("de/tr7zw/nbtapi/plugin/**")
    exclude("plugin.yml")
    relocate 'de.tr7zw', 'com.georgev22.library.minecraft.nbt'
    relocate 'com.cryptomorin.xseries', 'com.georgev22.library.minecraft.xseries'
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    ignoreFailures = false
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'
